set fallback := true

default:
    @just --list

clean-provider dir:
    #!/usr/bin/env bash
    dir="{{dir}}"

    rm -rf "./${dir}/.terraform"
    rm -rf "./${dir}/.terraform.lock.hcl"
    rm -rf "./${dir}/provider.json"
    rm -rf "./${dir}/gen"

pull-provider dir: (clean-provider dir)
    #!/usr/bin/env bash
    dir="{{dir}}"

    cd cmd/pull-provider
    go run main.go "../../${dir}"

gen-provider dir: (pull-provider dir)
    #!/usr/bin/env bash
    dir="{{dir}}"

    jsonnet-kit -J ./template/vendor manifest "./${dir}"

release-provider dir: (gen-provider dir)
    #!/usr/bin/env bash
    dir="{{dir}}"

    source="$(jsonnet -S -e "(import '${dir}/provider.json').source")"
    name="$(jsonnet -S -e "(import '${dir}/provider.json').name")"
    branch="terraform-provider/${source}"
    path="terraform-provider-${name}"
    jsonnet-kit release -r git@github.com:marcbran/jsonnet.git -b "${branch}" -p "${path}" "./${dir}/gen"
